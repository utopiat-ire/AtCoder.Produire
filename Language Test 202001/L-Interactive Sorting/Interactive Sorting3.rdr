※コンソール
コンソールから受け取って「 」で区切ってNQとする
N=NQ(1)
Q=NQ(2)
S={}
0からN-1までiを増やしながら繰り返す
	(「A」の文字番号+i)を文字化してSに追加する
繰り返し終わり
もしNが5なら
	Sを五ソートしてSとする
そうでなければ
	Sを1からNまでマージソートする
もし終わり
「! [Sを「」で繋げたもの]」を出力して改行する

【A】を【L:整数】から【R:整数】までマージソートする手順
	もしLがRなら返す
	【M:整数】=(L+(R-L)/2)を切り捨てる
	AをLからMまでマージソートする
	AをM+1からRまでマージソートする
	AをLからMでRまでマージする
終わり

【A】を【L:整数】から【M:整数】で【R:整数】までマージする手順
	【i:整数】=L
	【j:整数】=M+1
	【k:整数】=1
	【B】={}
	i<=Mかつj<=Rの間繰り返す
		もしA(i)がA(j)より、大きい値なら
			B(k)=A(j)
			jを増やす
		そうでなければ
			B(k)=A(i)
			iを増やす
		もし終わり
		kを増やす
	繰り返し終わり

	もしi=M+1なら
		j<=Rの間繰り返す
			B(k)=A(j)
			jを増やす
			kを増やす
		繰り返し終わり
	そうでなければ
		i<=Mの間繰り返す
			B(k)=A(i)
			iを増やす
			kを増やす
		繰り返し終わり
	もし終わり
	1からk-1までpを増やしながら繰り返す
		A(L+p-1)=B(p)
	繰り返し終わり
終わり


【A】を五ソートする手順
	もしA(1)がA(2)より、大きい値ならA(1)をA(2)と交換する
	もしA(3)がA(4)より、大きい値ならA(3)をA(4)と交換する
	もしA(1)がA(3)より、大きい値ならA={A(3),A(4),A(1),A(2),A(5)}
	もしA(3)がA(5)より、小さい値なら
		もしA(4)がA(5)より、大きい値ならA(4)をA(5)と交換する
		もしA(2)がA(4)より、小さい値なら
			もしA(2)がA(3)より、小さい値なら
				A={A(1),A(2),A(3),A(4),A(5)}
			そうでなければ
				A={A(1),A(3),A(2),A(4),A(5)}
			もし終わり
		そうでなければ
			もしA(2)がA(5)より、小さい値なら
				A={A(1),A(3),A(4),A(2),A(5)}
			そうでなければ
				A={A(1),A(3),A(4),A(5),A(2)}
			もし終わり
		もし終わり
	そうでなければ
		もしA(1)がA(5)より、小さい値なら
			もしA(2)がA(3)より、小さい値なら
				もしA(2)がA(5)より、小さい値なら
					A={A(1),A(2),A(5),A(3),A(4)}
				そうでなければ
					A={A(1),A(5),A(2),A(3),A(4)}
				もし終わり
			そうでなければ
				もしA(2)がA(4)より、小さい値なら
					A={A(1),A(5),A(3),A(2),A(4)}
				そうでなければ
					A={A(1),A(5),A(3),A(4),A(2)}
				もし終わり
			もし終わり
		そうでなければ
			もしA(2)がA(3)より、小さい値なら
				A={A(5),A(1),A(2),A(3),A(4)}
			そうでなければ
				もしA(2)がA(4)より、小さい値なら
					A={A(5),A(1),A(3),A(2),A(4)}
				そうでなければ
					A={A(5),A(1),A(3),A(4),A(2)}
				もし終わり
			もし終わり
		もし終わり
	もし終わり
	Aを返す
終わり

【X】が【Y】より、大きい値かどうかを判定する手順
	「? [X] [Y]」を出力して改行する
	コンソールで聞いて答えとする
	(答えが「>」)を返す
終わり
【X】が【Y】より、小さい値かどうかを判定する手順
	[XがYより、大きい値でない]を返す
終わり
