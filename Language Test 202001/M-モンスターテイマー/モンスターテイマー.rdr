※コンソール
コンソールから受け取って「 」で区切ってTNMとする
【T:整数】=TNM(1)
【N:整数】=TNM(2)
【M:整数】=TNM(3)
【討伐依頼リスト】={}
1からMまでiにカウントして繰り返す 
	コンソールから受け取って「 」で区切ってABCSとする
	クエスト(番号[i],開始[ABCS(1)],終了[ABCS(2)],報酬[ABCS(3)],要求レベル[ABCSの4番目から切り出したもの])を作ってQとする
	討伐依頼リストにQを追加する
	1からNまでjにカウントして繰り返す 
		Qの要求レベル(j)=Qの要求レベル(j)+0
	繰り返し終わり
繰り返し終わり
討伐依頼リストを『
	【左:クエスト】。【右:クエスト】
	それを{左,右}とみなす
	左の終了>右の終了なら
		-1を返す
	他で左の終了<右の終了なら
		1を返す
	そうでなければ
		0を返す
	もし終わり
』で並べ替える

【所持金:倍浮動小数】=1000
【スキル:整数の配列】は{}
【トレーニング:整数の配列】は{}
N回繰り返す
	0をスキルに追加する
	0をトレーニングに追加する
繰り返し終わり

1からTまでターンにカウントして繰り返す
	【選択スキル:整数】=0
	1からNまでiを増やしながら繰り返す
		スキル(i)が10未満かつ所持金が(10000*2^(スキル(i)+1))以上かつ(選択スキルが0またはスキル(i)<スキル(選択スキル))なら
			選択スキル=i
		もし終わり
	繰り返し終わり
/*
	1からNまでiを増やしながら繰り返す
		スキル(i)が10未満かつ所持金が(10000*2^(スキル(i)+1))以上かつ(選択スキルが0またはスキル(i)<スキル(選択スキル))なら
			選択スキル=i
		もし終わり
	繰り返し終わり
*/
	選択スキルが1以上なら
		「1 [選択スキル]」を出力して改行する
		所持金=所持金-10000*2^(スキル(選択スキル)+1)
		トレーニング(選択スキル)=トレーニング(選択スキル)+1
		トレーニング(選択スキル)がスキル(選択スキル)+1なら
			スキル(選択スキル)=スキル(選択スキル)+1
			トレーニング(選択スキル)=0
		もし終わり
	そうでなければ
		【選択討伐:整数】=0
		最大報酬=0
		1からMまでjを増やしながら繰り返す
			【依頼:クエスト】は、討伐依頼リスト(j)
			依頼が無なら繰り返しを続ける
			ターンが依頼の開始未満またはターンが依頼の終了より大きいなら繰り返しを続ける
			【r:長整数】=依頼:討伐報酬
			rが1000未満またはrが最大報酬より小さいなら繰り返しを続ける
			選択討伐=j
			最大報酬=r
			//繰り返しから抜ける
		繰り返し終わり
		選択討伐が1以上なら
			「2 [討伐依頼リスト(選択討伐)の番号]」を出力して改行する
			討伐依頼リスト(選択討伐)は、無
			所持金=所持金+最大報酬
		そうでなければ
			「3」を出力して改行する
			所持金=所持金+1000
		もし終わり
	もし終わり
繰り返し終わり

クエストとは
	+番号:整数
	+開始:整数
	+終了:整数
	+報酬:倍浮動小数
	+要求レベル:整数の配列={}
	
	討伐報酬を求める手順:長整数
		【結果:倍浮動小数】は、報酬*(1+9*(ターン-開始)/(終了-開始))
		【不足レベル:整数】=0
		【i:整数】
		1からNまでiを増やしながら繰り返す
			【必要レベル:整数】=要求レベル(i)
			スキル(i)が必要レベル未満なら
				不足レベル=不足レベル+必要レベル-スキル(i)
			もし終わり
		繰り返し終わり
		不足レベルが0なら
			結果=結果*10
		そうでなければ
			結果=結果*(1/2)^不足レベル
		もし終わり
		結果を切り捨てて返す
	終わり
終わり
